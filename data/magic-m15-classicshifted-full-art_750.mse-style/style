mse version: 2.1.2
game: magic
short name: M15 CS Art
full name: Classicshifted Art
icon: card-sample.png
position hint: 280

version: 2024-08-01
depends on:
	package: magic.mse-game
	version: 2014-06-25
depends on:
	package: magic-modules.mse-include
	version: 2024-05-20
depends on:
	package: magic-m15-classicshifted-shared.mse-include
	version: 2024-08-01
depends on:
	package: magic-mana-large.mse-symbol-font
	version: 2007-09-23
depends on:
	package: magic-mana-small.mse-symbol-font
	version: 2007-09-23

card width: 750
card height: 1046
card dpi: 300

### blanks by iDerp, tweaks by GenevensiS
### code by GenevensiS

############################################################## Extra scripts
init script:
	nameline_offset_top_1 := { if is_crowned() then -7 else -16 }
	casting_cost_offset_top_1 := { 1 }
	transform_symbol_offset_left_1 := { 18 }
	transform_symbol_offset_top_1 := { 14 }
	transform_symbol_offset_width_1 := { -28 }
	transform_symbol_offset_height_1 := { -28 }
	name_transform_symbol_offset_left_1 := { -18 }
	card_symbol_offset_left_1 := { -12 }
	card_symbol_offset_top_1 := { 5 }
	card_symbol_offset_width_1 := { -6 }
	card_symbol_offset_height_1 := { -6 }
	name_card_symbol_offset_left_1 := { 12 }
	
	typeline_offset_top_1 := { 4 }
	typeline_offset_left_1 := { 2 }
	rarity_disabled_1 := { true }
	
	card_stamp_offset_top_1 := { -2 }
	card_stamp_offset_height_1 := { 12 }
	card_stamp_offset_left_1 := { -6 }
	card_stamp_offset_width_1 := { 12 }
	
	swap_fonts_pt_default :=
	[
		name: {"Beleren Bold"},
		size: {30.0},
		color: {"white"},
		vertical: {0},
		italic: {"Beleren Bold"}
	]
	swap_fonts_body_default :=
	[
		name: {"MPlantin"},
		size: {28.0},
		color: {styling.rule_text_color},
		vertical: {0},
		italic: {"MPlantin-Italic"}
	]
	swap_fonts_type_default :=
	[
		name: {"Beleren Bold"},
		size: {24.0},
		color: {rgba(0,0,0,0)},
		vertical: {0},
		italic: {""}
	]
	swap_fonts_name_default :=
	[
		name: {"Beleren Bold"},
		size: {28.0},
		color: {"white"},
		vertical: {0},
		italic: {""}
	]
	
	template_prefix :=
	[
		card:			{ (if is_crowned() then "crown" else "card") + (if is_stamped() then "/" else "_nonstamp/") }
		pt:				{ "/magic-m15-classicshifted-shared.mse-include/pt/" }
		identity:		{ "/magic-modules.mse-include/indicators/" }
	]
	template_suffix :=
	[
		card:			{ "card.png" }
		pt:				{ "pt.png" }
		identity:		{ "identity.png" }
	]
	template :=			{ template_prefix[type]() + input + template_suffix[type]() }
	land_template :=	{ template_prefix[type]() + (if input == "a" then "c" else input) + "l" + template_suffix[type]() }
	
	card_crown :=		{ color_background(type:"crown",	base_hybrid:card_hybrid) }
	card_ptbox :=		{ color_background(type:"pt",		base_hybrid:card_hybrid) }
	
	card_mask :=
	{
		base := (if is_stamped() then "masks" else "masks_nonstamp") + (if is_extended() then "/extended.png" else "/full.png")
		if is_notched() then combine_blend(image1: base, image2: "masks/transform.png", combine: "subtract")
		else base
	}
	border_mask :=
	{
		(if is_stamped() then "masks" else "masks_nonstamp") + (if is_extended() then "/border_extended" else "/border_full") + (if is_crowned() then "_crown.png" else ".png")
	}
	
	textbox_background_image :=
	{
		color := styling.textbox_color
		alpha := get_alpha_percentage(styling.textbox_opacity_percentage, default: 0)
		set_alpha(
			recolor_image(
				"/magic-modules.mse-include/loyalty/color.png",
				color: color
			),
			alpha: alpha
		)
	}
	textbox_background_mask :=
	{
		base := (if is_stamped() then "masks" else "masks_nonstamp") + (if is_crowned() then "/textbox_crown" else "/textbox") + (if is_notched() then "_transform.png" else ".png")
		crop := crop_safe("masks/textbox_crop.png", width: 750, height: 1046, offset_x: 0, offset_y: 440 - chop_top(), max_x: 750, max_y: 2046)
		combine_blend(image1: base, image2: crop, combine: "multiply")
	}
	
	is_clear :=
	{
		styling.clear_frame == "yes" or
		(
			styling.clear_frame == "if colorless" and
			not lang_setting("is_artifact")(card.super_type) and
			not lang_setting("is_land")(card.super_type) and
			not contains(card.card_color, match: "white") and
			not contains(card.card_color, match: "blue") and
			not contains(card.card_color, match: "black") and
			not contains(card.card_color, match: "red") and
			not contains(card.card_color, match: "green") and
			not contains(card.card_color, match: "land") and
			not contains(card.card_color, match: "artifact") and
			not contains(card.card_color, match: "multicolor")
		)
	}
	is_bordered := { styling.border }
	is_extended := { styling.extended_art }
	is_crowned := { styling.legend_crown == "yes" or (styling.legend_crown == "if legendary" and lang_setting("is_legendary")(card.super_type)) }
	is_nyx := { styling.nyx_frame == "yes" or (styling.nyx_frame == "if enchantment" and lang_setting("is_nyx")(card.super_type)) }
	is_vehicle := { styling.vehicle_frame == "yes" or (styling.vehicle_frame == "if vehicle" and lang_setting("is_vehicle")(card.sub_type)) }
	is_miracle := { styling.miracle_frame == "yes" or (styling.miracle_frame == "if miracle" and lang_setting("is_miracle")(card.text)) }
	is_notched := { styling.transformation_reminder }
	has_identity := { styling.color_indicator_dot }
	has_identity_2 := { false }
	has_identity_3 := { false }
	is_basic_symboled := { styling.basic_land_symbol != "none" }
	
	chop_top :=			{ split := split_comma(styling.chop_text_box); if length(split) > 0 and split.0 != "" then clamp(split.0, maximum: 300, minimum: -560) else 0 }
	chop_bot :=			{ split := split_comma(styling.chop_text_box); if length(split) > 1 and split.1 != "" then clamp(split.1, maximum: 500, minimum: -500) else 0 }
	
	popout_left :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.0 != "" and split.0 != "-" then clamp(split.0, maximum:1500, minimum:-500) else 0 }
	popout_top :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.1 != "" and split.1 != "-" then clamp(split.1, maximum:2000, minimum:-500) else 0 }
	popout_width :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.2 != "" and split.2 != "-" then clamp(split.2, maximum:1500, minimum:0) else 0 }
	popout_height :=	{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.3 != "" and split.3 != "-" then clamp(split.3, maximum:2000, minimum:0) else 0 }

############################################################## Extra style options
styling field:
	type:			text
	name:			popout art coordinates
	description:	The coordinates for the popout image field, formatted as "left,top,width,height". Used to make the illustration jump in front of the frame.
styling field:
	type:			boolean
	name:			stretch art to whole card
	description:	Make the image span the whole card instead of just the visible part. Use this to help make popout effects.
	initial:		no
styling field:
	type:			text
	name:			textbox opacity percentage
	description:	Set the opacity percentage for textboxes. Opaque is 100, transparent is 0, default is 0.
styling field:
	type:			color
	name:			textbox color
	description:	Use the specified color for the textbox background.
	choice:
		name:			black
		color:			rgb(0,0,0)
	choice:
		name:			white
		color:			rgb(255,255,255)
	initial:		rgb(255,255,255)
styling field:
	type:			color
	name:			rule text color
	description:	Use the specified color for the rule text.
	choice:
		name:			black
		color:			rgb(0,0,0)
	choice:
		name:			white
		color:			rgb(255,255,255)
	initial:		rgb(0,0,0)

include file: /magic-modules.mse-include/texts/styling_fields

styling field:
	type:			boolean
	name:			transformation reminder
	description:	Add a transformation notch and p/t
	initial:		no
styling field:
	type:			choice
	name:			legend crown
	description:	Use the legend crown?
	choice:			yes
	choice:			if legendary
	choice:			no
	initial:		if legendary
styling field:
	type:			boolean
	name:			extended art
	description:	Make the illustration span the entire width of the card?
	initial:		no
styling field:
	type:			choice
	name:			clear frame
	description:	Use a see-through frame?
	choice:			yes
	choice:			if colorless
	choice:			no
	initial:		if colorless
styling field:
	type:			choice
	name:			vehicle frame
	description:	Use the vehicle frame overlay?
	choice:			yes
	choice:			if vehicle
	choice:			no
	initial:		if vehicle
styling field:
	type:			choice
	name:			nyx frame
	description:	Use the nyx frame overlay?
	choice:			yes
	choice:			if enchantment
	choice:			no
	initial:		if enchantment
styling field:
	type:			choice
	name:			miracle frame
	description:	Use the miracle frame overlay?
	choice:			yes
	choice:			if miracle
	choice:			no
	initial:		if miracle

include file: /magic-modules.mse-include/rarities/styling_fields

include file: /magic-modules.mse-include/indicators/styling_fields

include file: /magic-modules.mse-include/watermarks/styling_fields

styling field:
	type:			choice
	name:			basic land symbol
	description:	Use a basic land symbol in place of the stamp
	choice:			none
	choice:			plains
	choice:			island
	choice:			swamp
	choice:			mountain
	choice:			forest
	choice:			wastes
styling field:
	type:			package choice
	name:			overlay
	description:	Should there be an overlay applied, such as foil?
	match:			magic-overlay-*.mse-include
	required:		false
styling field:
	type:			boolean
	name:			border
	description:	Use a border around card?
	initial:		yes

include file: /magic-modules.mse-include/information/styling_fields

include file: /magic-modules.mse-include/symbol-fonts/styling_fields

include file: /magic-modules.mse-include/fonts/styling_fields

############################################################## Card fields

include file: /magic-modules.mse-include/information/card_fields

include file: /magic-modules.mse-include/stamps/card_fields

include file: /magic-modules.mse-include/corners/card_fields

include file: /magic-modules.mse-include/watermarks/card_fields

include file: /magic-modules.mse-include/separators/card_fields

include file: /magic-modules.mse-include/namelines/card_fields

include file: /magic-modules.mse-include/typelines/card_fields

card style:
	############################# Background stuff
	border color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			1
		visible:			{ is_bordered() }
		mask:				{ border_mask() }
	card color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			2
		popup style:		in place
		render style:		image
		image:				{ if is_clear() then "" else card_background() }
		mask:				{ card_mask() }
	
	############################# Image
	image:
		left:				{ if styling.stretch_art_to_whole_card or (not is_bordered()) or is_extended()	then 0						else if is_crowned() and is_clear() then 11		else if is_clear() then 29									else 60 }
		top:				{ if styling.stretch_art_to_whole_card or (not is_bordered())					then 0						else if is_crowned() and is_clear() then 15		else if is_clear() then 30	else if is_crowned() then 105	else 97 }
		right:				{ if styling.stretch_art_to_whole_card or (not is_bordered()) or is_extended()	then stylesheet.card_width	else if is_crowned() and is_clear() then 739	else if is_clear() then 721									else 691 }
		bottom:				{ if styling.stretch_art_to_whole_card or (not is_bordered())					then stylesheet.card_height	else if is_crowned() and is_clear() then 971	else if is_clear() then 971									else 586 }
		z index:			0
	mainframe image:
		left:				{ popout_left() }
		top:				{ popout_top() }
		width:				{ popout_width() }
		height:				{ popout_height() }
		z index:			1000
	
	############################# Text box
	text:
		left:				66
		top:				{ 667 + chop_top() + (if is_stamped() then 4 else 0) + body_font_vertical() }
		right:				684
		bottom:				{ 951 - chop_bot() - (if is_stamped() then 8 else 0) }
		z index:			900
		line height hard:	{ if is_modal(card.rule_text) then 0.9 else 1.2 }
		line height line:	1.5
		line height soft:	0.9
		line height hard max:	{ if is_modal(card.rule_text) then 1.0 else 1.3 }
		line height line max:	1.6
		alignment:
			script:
				if
				(
					styling.center_text == "short text only" and
					not contains(match:"\n", card.rule_text) and
					to_text(card.flavor_text) == "" and
					card_style.text.content_lines <= 2
				)
				or	styling.center_text == "always"
				then "middle center"
				else "middle left"
		font:
			name:				{ body_font() }
			italic name:		{ body_font_italic() }
			size:				{ body_font_size() }
			scale down to:		7
			color:				{ body_font_color() }
			shadow color:			{ if styling.rule_text_color == rgb(255,255,255) then rgb(0,0,0) else rgb(128,128,128) }
			shadow displacement x:	2
			shadow displacement y:	2
		symbol font:
			name:				{ styling.text_box_mana_symbols }
			size:				{ body_font_size() }
			scale down to:		7
	rule text 3:
		left:				589
		top:				{ 899 + (if card.pt == "" then 2 else 0) + body_font_vertical() }
		width:				{ if is_notched() then 100 else 0 }
		height:				30
		z index:			920
		alignment:			right middle
		font:
			name:				{ body_font() }
			italic name:		{ body_font_italic() }
			size:				23.5
			color:				{ body_font_color() }
	
	############################# PT
	pt:
		left:				598
		top:				{ 935 + pt_font_vertical() }
		width:				94
		height:				60
		z index:			910
		alignment:			center middle shrink-overflow
		font:
			name:				{ pt_font() }
			size:				{ pt_font_size() }
			color:				{ pt_font_color() }
			separator color:	red
			shadow color:			rgb(0,0,0)
			shadow displacement x:	2
			shadow displacement y:	2

############################################################## Extra card fields
extra card field:
	type:				choice
	name:				basic symbol
	editable:			false
	save value:			false
	choice:				basic symbol
extra card field:
	type:				choice
	name:				transform notch
	editable:			false
	save value:			false
	script:				card.card_color
extra card field:
	type:				choice
	name:				pt box
	editable:			false
	save value:			false
	script:				card.card_color
extra card field:
	type:				choice
	name:				miracle overlay
	editable:			false
	save value:			false
	script:				card.card_color
extra card field:
	type:				choice
	name:				nyx overlay
	editable:			false
	save value:			false
	script:				card.card_color
extra card field:
	type:				choice
	name:				vehicle overlay
	editable:			false
	save value:			false
	script:				card.card_color
extra card field:
	type:				choice
	name:				devoid card mask
	editable:			false
	save value:			false
	choice:				devoid card mask
extra card field:
	type:				choice
	name:				devoid frame
	editable:			false
	save value:			false
	choice:				devoid frame
extra card field:
	type:				choice
	name:				devoid frame 2
	editable:			false
	save value:			false
	choice:				devoid frame 2
extra card field:
	type:				choice
	name:				textbox background
	editable:			false
	save value:			false
	show statistics:	false
	choice:				textbox background
extra card style:
	basic symbol:
		left:				310
		top:				895
		width:				130
		height:				130
		z index:			900
		visible:			{ is_basic_symboled() }
		render style:		image
		image:
			script:
				if		styling.basic_land_symbol == "plains"	then	"badge/wbadge.png"
				else if	styling.basic_land_symbol == "island"	then	"badge/ubadge.png"
				else if	styling.basic_land_symbol == "swamp"	then	"badge/bbadge.png"
				else if	styling.basic_land_symbol == "mountain"	then	"badge/rbadge.png"
				else if	styling.basic_land_symbol == "forest"	then	"badge/gbadge.png"
				else													"badge/cbadge.png"
	transform notch:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			3
		visible:			{ is_notched() and not is_clear() }
		render style:		image
		image:				transform.png
		mask:				{ card_mask() }
	pt box:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			19
		visible:			{ card.pt != "" }
		render style:		image
		image:				{ if is_clear() then "/magic-m15-classicshifted-shared.mse-include/pt/cpt.png" else card_ptbox() }
	nyx overlay:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			5
		visible:			{ is_nyx() }
		render style:		image
		image:				{ if is_crowned() then set_combine(input: "/magic-m15-classicshifted-shared.mse-include/nyx/nyx_crown_extended.png", combine: "add") else set_combine(input: "/magic-m15-classicshifted-shared.mse-include/nyx/nyx_extended.png", combine: "add") }
		mask:				{ (if is_extended() then "masks/extended" else "masks/full") + (if is_stamped() then "_stamp.png" else ".png") }
	vehicle overlay:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			6
		visible:			{ is_vehicle() }
		render style:		image
		image:				/magic-m15-classicshifted-shared.mse-include/vehicle/vehicle_extended.png
		mask:				{ (if is_extended() then "masks/extended" else "masks/full") + (if is_stamped() then "_stamp.png" else ".png") }
	miracle overlay:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			7
		visible:			{ is_miracle() }
		render style:		image
		image:				{ if is_crowned() then set_alpha(set_combine(input: "/magic-m15-classicshifted-shared.mse-include/miracle/miracle_crown_extended.png", combine: "add"), alpha:0.6) else set_alpha(set_combine(input: "/magic-m15-classicshifted-shared.mse-include/miracle/miracle_extended.png", combine: "add"), alpha:0.6) }
		mask:				{ (if is_extended() then "masks/extended" else "masks/full") + (if is_stamped() then "_stamp.png" else ".png") }
	#devoid card mask:
	#	left:				0
	#	top:				0
	#	width:				{ stylesheet.card_width }
	#	height:				{ stylesheet.card_height }
	#	z index:			1
	#	visible:			{ is_clear() }
	#	render style:		image
	#	image:				{ image := (if is_stamped() then "clear_masks" else "clear_masks_nonstamp") + (if is_crowned() then "/devoid_image_mask_crown" else "/devoid_image_mask") + (if is_notched() then "_transform.png" else ".png"); set_combine(image, combine: "multiply") }
	devoid frame:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			2
		visible:			{ is_clear() }
		render style:		image
		image:				{ image := (if is_stamped() then "clear_masks" else "clear_masks_nonstamp") + (if is_extended() then "/devoid_extended" else "/devoid") + (if is_crowned() then "_crown" else "") + (if is_notched() then "_transform.png" else ".png"); set_combine(image, combine: "multiply") }
		mask:				{ (if is_stamped() then "clear_masks" else "clear_masks_nonstamp") + (if is_extended() then "/devoid_mask_extended" else "/devoid_mask") + (if is_crowned() then "_crown" else "") + (if is_notched() then "_transform.png" else ".png") }
	devoid frame 2:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			3
		visible:			{ is_clear() }
		render style:		image
		image:				{ image := (if is_stamped() then "clear_masks" else "clear_masks_nonstamp") + (if is_extended() then "/devoid_screen_extended" else "/devoid_screen") + (if is_crowned() then "_crown" else "") + (if is_notched() then "_transform.png" else ".png"); set_alpha(set_combine(image, combine: "screen"), alpha:0.2) }
		mask:				{ (if is_stamped() then "clear_masks" else "clear_masks_nonstamp") + (if is_extended() then "/devoid_mask_extended" else "/devoid_mask") + (if is_crowned() then "_crown" else "") + (if is_notched() then "_transform.png" else ".png") }
	textbox background:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			18
		render style:		image
		image:				{ textbox_background_image() }
		mask:				{ textbox_background_mask() }
