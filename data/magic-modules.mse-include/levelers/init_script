old_template := template
template :=
{
	if		type == "pt"		then leveler_pt_box_image_1(1, color: input)
	else if	type == "pt2"		then leveler_pt_box_image_1(2, color: input)
	else if	type == "pt3"		then leveler_pt_box_image_1(3, color: input)
	else if	type == "pt4"		then leveler_pt_box_image_1(4, color: input)
	else if	type == "level"		then leveler_level_box_image_1(1, color: input)
	else if	type == "level2"	then leveler_level_box_image_1(2, color: input)
	else if	type == "level3"	then leveler_level_box_image_1(3, color: input)
	else if	type == "level4"	then leveler_level_box_image_1(4, color: input)
	else old_template()
}
old_land_template := land_template
land_template :=
{
	if		type == "pt"		then leveler_pt_box_land_image_1(1, color: input)
	else if	type == "pt2"		then leveler_pt_box_land_image_1(2, color: input)
	else if	type == "pt3"		then leveler_pt_box_land_image_1(3, color: input)
	else if	type == "pt4"		then leveler_pt_box_land_image_1(4, color: input)
	else if	type == "level"		then leveler_level_box_land_image_1(1, color: input)
	else if	type == "level2"	then leveler_level_box_land_image_1(2, color: input)
	else if	type == "level3"	then leveler_level_box_land_image_1(3, color: input)
	else if	type == "level4"	then leveler_level_box_land_image_1(4, color: input)
	else old_land_template()
}
alt_text := { true }
alt_text_script :=
{
	count := leveler_abilities_count_1()
	if		count == 4 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text, separator2: "<line>\n</line>", field3: card.level_3_text, separator3: "<line>\n</line>", field4: card.level_4_text)
	else if	count == 3 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text, separator2: "<line>\n</line>", field3: card.level_3_text)
	else if	count == 2 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text)
	else					forward_editor(field: card.level_1_text)
}
margin_left :=
{
	number := to_int(leveler_clean_margin_code(input)) or else 999
	if number > 4 or card["level_" + (number-1)] == "" then 0
	else to_int(190 * face_coordinates_map(leveler_face_1()).width/375 + (leveler_level_offset_left_1() + leveler_level_offset_width_1() + leveler_level_offset_text_margin_1()) * 10)
}
margin_right :=
{
	number := to_int(leveler_clean_margin_code(input)) or else 999
	if number > 4 or (number == 1 and card.pt == "") or card["pt_" + number] == "" then 0
	else to_int(150 * face_coordinates_map(leveler_face_1()).width/375 - (leveler_pt_offset_left_1() + leveler_pt_offset_width_1() - leveler_pt_offset_text_margin_1()) * 10)
}
add_spacers :=
{
	#The 'then ""' has a zero-width space inside
	if input == "" and leveler_level_is_active_1(name) then "â€‹"
	else input
}
text_filter := text_filter + { apply_margins(input, name: margin_code) } + { add_spacers(input, name: margin_code) }
